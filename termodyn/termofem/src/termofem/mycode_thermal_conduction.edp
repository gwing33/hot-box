verbosity = 0;
// Parameters
func u0 = 10. + 90. * x / 6.;
//func u0 = 10.;
func k = 1.8 * (y < 0.5) + 0.2;
real ue = 25.;
real alpha=0.25;
real T=55.;
real dt=0.1 ;

// Mesh
mesh Th = square(30, 5, [6.*x,y]);
//plot(Th, wait=true);

// Fespace
fespace Vh(Th, P2);
Vh u=u0, v, uold;

// Problem
problem thermic(u, v)
    = int2d(Th)(
          u*v/dt
        + k*(
              dx(u) * dx(v)
            + dy(u) * dy(v)
        )
    )
    - int2d(Th)(
          uold*v/dt
    )
    + int1d(Th, 1, 3)(
             alpha*u*v
    )
    - int1d(Th, 1, 3)(
          alpha*ue*v
    )
    + on(2, 4, u=u0)
    ;

// Time iterations
ofstream ff("thermic.dat");
for(real t = 0; t < T; t += dt){
    uold = u; //equivalent to u^{n-1} = u^n
    thermic; //here the thermic problem is solved
    ff << u(3., 0.5) << endl;
//    for(int i = 0; i < 20; i++)
//        cout << dy(u)(6.0*i/20.0,0.9) << endl;
    plot(u, fill=true, value=true);
}
