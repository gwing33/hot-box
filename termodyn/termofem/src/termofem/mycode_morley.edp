load "Morley"

// Parameters
int nn = 10;
real h = 0.01;
real f = 1;

// Mesh
mesh Th = square(nn, nn);
Th = adaptmesh(Th, h, IsMetric=1);

int NbBoundaryElements = Th.nbe;
for (int k = 0; k < NbBoundaryElements; k++)
    cout << endl
        << "Boundary Element: " << k << " : " << Th.be(k)[0] << " " << Th.be(k)[1]
        << " , label " << Th.be(k).label
        << ", triangle " << int(Th.be(k).Element)
        << " " << Th.be(k).whoinElement << endl;


// Fespace
fespace Vh(Th, P2Morley); //The Morley finite element space
Vh [u, ux, uy], [v, vx, vy];

// Macro
macro bilaplacien(u, v) (dxx(u)*dxx(v) + dyy(u)*dyy(v) + 2.*dxy(u)*dxy(v)) //

// Problem
solve bilap ([u, ux, uy], [v, vx, vy])
    = int2d(Th)(
        bilaplacien(u, v)
    )
    - int2d(Th)(
        f*v
    )
    + on(1, 2, 3, 4, u=0, ux=0, uy=0)
    ;

// Plot
plot(u, cmm="u");
